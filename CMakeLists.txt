cmake_minimum_required(VERSION 3.8)
project(FixingCollisions)

################################################################################

# libigl-options
option(LIBIGL_USE_STATIC_LIBRARY        "Use libigl as static library" OFF)
option(LIBIGL_WITH_OPENGL               "Use OpenGL"                   ON)
option(LIBIGL_WITH_OPENGL_GLFW          "Use GLFW"                     ON)
option(LIBIGL_WITH_OPENGL_GLFW_IMGUI    "Use IMGUI"                    ON)

# project-options
option(BUILD_UNIT_TESTS         "Build unit-tests"     ON)
option(BUILD_SCRIPTS            "Build scripts"        ON)
option(ENABLE_DERIVATIVE_CHECK  "Check Derivatives"    ON)
option(ENABLE_PROFILING         "Profile Functions"    ON)
option(ENABLE_IPOPT             "Enable Ipopt Library" OFF)
option(ENABLE_OSQP              "Enable OSQP Library"  ON)
option(ENABLE_MOSEK             "Enable Mosek Library" OFF)
option(BUILD_COMPARISONS        "Build comparisons"    ON)
option(CODE_COVERAGE            "Builds targets with code coverage instrumentation. (Requires GCC or Clang)" OFF)

# debug-options
option(DEBUG_COLLISIONS         "Extra logs in newton" OFF)
option(DEBUG_LINESEARCH         "Extra logs in linesearch" OFF)
option(DEBUG_COR                "Extra logs in restitution" OFF)

################################################################################

### Configuration
set(FIXING_COLLISIONS_EXTERNAL "${CMAKE_CURRENT_SOURCE_DIR}/3rdparty")
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${CMAKE_CURRENT_SOURCE_DIR}/cmake)

include(Autogen)
include(Warnings)
include(UseColors)
include(${PROJECT_NAME}Dependencies)

################################################################################
# Fixing Collisions Library
################################################################################

add_library(${PROJECT_NAME}
    src/autodiff/finitediff.cpp
    src/autodiff/autodiff.cpp

    src/ccd/degenerate_edge_error.cpp
    src/ccd/impact.cpp
    src/ccd/hash_grid.cpp
    src/ccd/collision_detection_narrow_phase.cpp
    src/ccd/collision_detection_broad_phase.cpp
    src/ccd/prune_impacts.cpp
    src/ccd/time_of_impact.cpp

    src/io/read_scene.cpp
    src/io/write_scene.cpp
    src/io/opt_results.cpp
    src/io/serialize_json.cpp
    src/io/read_rb_scene.cpp

    src/physics/mass_matrix.cpp
    src/physics/center_of_mass.cpp
    src/physics/moment_of_inertia.cpp
    src/physics/simulation_problem.cpp
    src/physics/particles_problem.cpp
    src/physics/rigid_body_problem.cpp
    src/physics/rigid_body.cpp
    src/physics/rigid_body_assembler.cpp


    src/opt/barrier.cpp
    src/opt/barrier_chorner.cpp
    src/opt/distance_barrier_constraint.cpp
    src/opt/collision_constraint.cpp
    src/opt/optimization_problem.cpp
    src/opt/optimization_results.cpp
    src/opt/volume_constraint.cpp

    src/solvers/barrier_solver.cpp
    src/solvers/ipopt_solver.cpp
    src/solvers/lcp_solver.cpp
    src/solvers/ncp_solver.cpp
    src/solvers/line_search.cpp
    src/solvers/newton_solver.cpp
    src/solvers/gradient_descent_solver.cpp
    src/solvers/bfgs_solver.cpp
    src/solvers/nlopt_solver.cpp
    src/solvers/optimization_solver.cpp
    src/solvers/qp_solver.cpp
    src/solvers/solver_factory.cpp

    src/problems/distance_barrier_rb_problem.cpp
    src/problems/distance_barrier_particles_problem.cpp
    src/problems/volume_rb_problem.cpp
    src/problems/volume_particles_problem.cpp
    src/problems/problem_factory.cpp

    src/utils/tensor.cpp
    src/utils/regular_2d_grid.cpp

    src/SimState.cpp
    src/logger.cpp
    src/profiler.cpp

    # Generated files
    src/autogen/time_of_impact_coeff.tpp
    src/autogen/collision_volume.tpp

)

target_include_directories(${PROJECT_NAME} PUBLIC src)

# Use C++14
target_compile_features(${PROJECT_NAME} PUBLIC cxx_std_14)

# Autogenerated files ----------------------------------------------------------
project_autogen(${PROJECT_NAME} time_of_impact_coeff.py time_of_impact_coeff.tpp auto_time_of_impact_coeff .ipp)
project_autogen(${PROJECT_NAME} collision_volume.py collision_volume.tpp auto_collision_volume .ipp)
#target_sources(${PROJECT_NAME} PRIVATE
#    src/autogen/auto_collision_volume.cpp
#    src/autogen/auto_time_of_impact_coeff.cpp
#)

# Directory paths
set(OPT_OUTPUT_DIR "${CMAKE_CURRENT_SOURCE_DIR}/figures/data")
target_compile_definitions(${PROJECT_NAME} PUBLIC -DOPT_OUTPUT_DIR=\"${OPT_OUTPUT_DIR}\")

set(FIXTURES_DIR ${CMAKE_CURRENT_SOURCE_DIR}/fixtures)
target_compile_definitions(${PROJECT_NAME} PUBLIC -DFIXTURES_DIR=\"${FIXTURES_DIR}\")

set(DATA_OUTPUT_DIR "${CMAKE_CURRENT_SOURCE_DIR}/data")
target_compile_definitions(${PROJECT_NAME} PUBLIC -DDATA_OUTPUT_DIR=\"${DATA_OUTPUT_DIR}\")

set(LOGS_OUTPUT_DIR "${CMAKE_CURRENT_SOURCE_DIR}/logs")
target_compile_definitions(${PROJECT_NAME} PUBLIC -DLOGS_OUTPUT_DIR=\"${LOGS_OUTPUT_DIR}\")

if(ENABLE_DERIVATIVE_CHECK)
    target_compile_definitions(${PROJECT_NAME} PUBLIC -DWITH_DERIVATIVE_CHECK)
endif()

if(DEBUG_LINESEARCH)
    target_compile_definitions(${PROJECT_NAME} PUBLIC -DDEBUG_LINESEARCH)
endif()
if(DEBUG_COR)
    target_compile_definitions(${PROJECT_NAME} PUBLIC -DDEBUG_COR)
endif()
if(DEBUG_COLLISIONS)
    target_compile_definitions(${PROJECT_NAME} PUBLIC -DDEBUG_COLLISIONS)
endif()

if(ENABLE_PROFILING)
    message("Profiling Enabled")
    target_compile_definitions(${PROJECT_NAME} PUBLIC -DPROFILE_FUNCTIONS)
endif()

################################################################################
# Simulations Executable
################################################################################

add_executable(${PROJECT_NAME}_sim
    src/viewer/imgui_ext.cpp
    src/viewer/igl_viewer_ext.cpp
    src/viewer/UISimState.cpp
    src/viewer/UIMenu.cpp

    src/main.cpp
)

target_include_directories(${PROJECT_NAME}_sim PRIVATE src)
target_link_libraries(${PROJECT_NAME}_sim PUBLIC ${PROJECT_NAME})

# Fixtures folder --------------------------------------------------------------
add_custom_command(TARGET ${PROJECT_NAME}_sim POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy_directory
        ${CMAKE_SOURCE_DIR}/fixtures
        $<TARGET_FILE_DIR:${PROJECT_NAME}_sim>/fixtures)

################################################################################
# No UI Executable
################################################################################
add_executable(${PROJECT_NAME}_ngui
     src/main2.cpp
)

target_include_directories(${PROJECT_NAME}_ngui PRIVATE src)
target_link_libraries(${PROJECT_NAME}_ngui PUBLIC ${PROJECT_NAME})
target_link_libraries(${PROJECT_NAME}_ngui PUBLIC CLI11::CLI11)

################################################################################
# MinDistance Executable
################################################################################
add_executable(cli_mindistance
     src/cli_min_distance.cpp
)

target_include_directories(cli_mindistance PRIVATE src)
target_link_libraries(cli_mindistance PUBLIC ${PROJECT_NAME})
target_link_libraries(cli_mindistance PUBLIC CLI11::CLI11)

################################################################################
# Required Libraries
################################################################################

# libigl
target_link_libraries(${PROJECT_NAME} PUBLIC igl::core igl::opengl igl::opengl_glfw igl::opengl_glfw_imgui)

# Extra warnings
target_link_libraries(${PROJECT_NAME} PRIVATE warnings::all)

# Json Parser
target_link_libraries(${PROJECT_NAME} PUBLIC nlohmann_json::nlohmann_json)

# NLopt
target_link_libraries(${PROJECT_NAME} PUBLIC nlopt::nlopt)

# Logger
target_link_libraries(${PROJECT_NAME} PUBLIC spdlog::spdlog)

#  GMP
#find_package(GMP)
#if(NOT ${GMP_FOUND})
#        MESSAGE(FATAL_ERROR "Cannot find GMP")
#endif()
#target_include_directories(${PROJECT_NAME} PUBLIC ${GMP_INCLUDE_DIRS})
#target_link_libraries(${PROJECT_NAME} PUBLIC ${GMP_LIBRARIES})
#

# Ipopt
if(ENABLE_IPOPT)
    find_package(IPOPT)
    if (IPOPT_FOUND)
        message(STATUS "Including IPOPT")
        target_compile_definitions(${PROJECT_NAME} PUBLIC -DBUILD_WITH_IPOPT)
        target_include_directories(${PROJECT_NAME} SYSTEM PUBLIC ${IPOPT_INCLUDE_DIR} )
        link_directories(${IPOPT_LIBRARY_DIR})
        target_link_libraries(${PROJECT_NAME} PUBLIC ${IPOPT_LIBRARY})
    endif()
endif()

# OSQP
if(ENABLE_OSQP)
    target_compile_definitions(${PROJECT_NAME} PUBLIC -DBUILD_WITH_OSQP)
    target_link_libraries(${PROJECT_NAME} PUBLIC osqp::osqp)
endif()

# Mosek
if(ENABLE_MOSEK)
    target_compile_definitions(${PROJECT_NAME} PUBLIC -DBUILD_WITH_MOSEK)
    target_link_libraries(${PROJECT_NAME} PUBLIC mosek::mosek)
endif()

################################################################################
# Tests
################################################################################

if(CODE_COVERAGE)
    message(STATUS "Building with code coverage")
    include(CodeCoverage)
    add_code_coverage_all_targets()

    target_code_coverage(${PROJECT_NAME})
#    target_code_coverage(${PROJECT_NAME}_bin)
    target_code_coverage(${PROJECT_NAME}_sim)
    target_code_coverage(${PROJECT_NAME}_ngui)
endif()

if(BUILD_UNIT_TESTS)
    # Catch2
    if(NOT TARGET Catch2::Catch2)
        download_catch2()
        add_subdirectory(${FIXING_COLLISIONS_EXTERNAL}/Catch2 catch2)
        list(APPEND CMAKE_MODULE_PATH ${FIXING_COLLISIONS_EXTERNAL}/Catch2/contrib)
    endif()

    include(CTest)
    enable_testing()
    add_subdirectory(tests)
endif()

################################################################################
# Scripts
################################################################################

if(BUILD_SCRIPTS)
#    add_subdirectory(scripts)
endif()

################################################################################
# Comparisons to other methods
################################################################################
if(BUILD_COMPARISONS)
    add_subdirectory(comparisons)
endif()
